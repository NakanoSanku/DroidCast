apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.rayworks.droidcast"
        minSdkVersion 14
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders += [APP_ICON: "@mipmap/ic_launcher"]
        manifestPlaceholders += [APP_ROUND_ICON: "@mipmap/ic_launcher_round"]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "product", "level"

    productFlavors {
        paid {
            dimension "product"
        }
        free {
            dimension "product"
        }

        advance {
            dimension "level"
        }

        basic {
            dimension "level"
        }
    }
}

androidComponents {
    onVariants(selector().withBuildType("debug")) {
        println(">>> " + it.flavorName)

        def flavor = it.flavorName.toLowerCase()
        if (flavor.contains("free") && flavor.contains("advance")) {
            println(">>> " + it.flavorName + " matched!")
            it.manifestPlaceholders.APP_ICON = "@mipmap/ic_launcher_new"
            it.manifestPlaceholders.APP_ROUND_ICON = "@mipmap/ic_launcher_round_new"
        }
    }
}

dependencies {
    compileOnly('com.android.tools.build:gradle:4.2.1')

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.core:core-ktx:1.5.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'junit:junit:4.13.2'

    implementation 'com.koushikdutta.async:androidasync:3.1.0'
}

apply from: "../artifacts.gradle"
